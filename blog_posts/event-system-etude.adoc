:imagesdir: ./blog_posts/event-system-etude
:source-highlighter: pygments
:pygments-style: default
:pygments-css: style
:pygments-linenums-mode: inline
:toc:

== What are events?

Events are happenings, state changes, that are recorded and delivered to interested entities.

.Example events/who they are delivered to
* a collision between an enemy and a bullet occurs
** enemy - the enemy would take damage
** bullet - the bullet would then expire
** score system/hit counter - the hit percentage would increase
* a player walks into water
** all underwater enemies within range
** sound effect system - a splash sound effect is played
** player - the player's air meter starts depleting
* the fire button is pressed
** player/gun - the gun fires a bullet
* any event
** the logging/debugging system

NOTE: In addition to delivering messages to entities, events can be delivered to subsystems.

=== Events as Commands

Events can also act as commands:

* a command to a character tells them to jump or say a line of text
* a command to a sound system, to play a sound effect
* a command to a music system, to pause the music

== Event Implementation

Events need to record what happened: it's type, and the details of what happened: it's data.

In Lisp we can do something really simple, using `defstruct`:

.Some example event structures.
[source,lisp,linenums]
----
(defstruct event-quit) ; example of event with no data

(defstruct event-bullet-collision
  entity
  bullet)
(defstruct event-button-press
  button)
----

== Event Handling

A dead-simple implementation of event-handling is to forward each and every event to all entities.
Let each entity decide which events to respond to and how to respond to them.
Supposing we have:

* a list of all entities and subsystems, `\*entities*`, 
* a method `(entity-handle-event entity event)` implemented by each entity/subsystem


.Example of how `handle-event` could be implemented.
[source,lisp,linenums]
----
(defun handle-event (event)
  (setq *entities*
        (mapcar (lambda (entity) (entity-handle-event entity event))
                *entities*)))
----

For the event handling methods, We can dispatch on the event type using `typecase`.

.Example of how entity-handle-event could be implemented for a player entity.
[source,lisp,linenums]
----
(defmethod entity-handle-event ((player player) event)
  (typecase event
    (event-bullet-collision
     (when (eql (event-bullet-collision-entity event) player)
       ;; take damage
       ))
    (event-button-press
     (when (eql (event-button-press-button event) :fire)
       ;; fire bullet
       ))))
----

== Event Queue

Systems and entities need some way of _actually creating the events_.
Typically you want the events to be delivered in the order that they occur.
This keeps things chronologically consistent.

[source,lisp,linenums]
----
(defvar *events*)
(defun event-push! (event)
  (push event *events*))
----

I've chosen to implement the event queue as a stack.
We can remove all of the events at once, and reverse them.

[source,lisp,linenums]
----
(defun deliver-events! ()
  ;; Handle the events in the order they were pushed
  (let ((events (reverse *events*)))
    ;; Clear/save the old list of events before handling them
    ;; in case handle-event creates new events.
    (setq *events* ())
    (dolist (event events)
      (handle-event event))))
----

=== Events from Event Handlers

`deliver-events!` doesn't deliver events being added from event handlers, only what was already on the event queue.
It would be very useful to handle new events as we get them.

.Keep calling `deliver-events!` until the event-handlers stop adding new events.
[source,lisp,linenums]
----
(defun handle-events ()
  (let ((num-iterations 0))
    (loop until (or (null *events*)
                    (> num-iterations 10)) do
         (deliver-events!)
         (incf num-iterations))

    (when (> num-iterations 10)
      (warn "handle-events: too many back-and-forth events: ~A" *events*))))
----

I cap the number of times we call `deliver-events!` to avoid infinite cycles.
I consider it an at-best sub-par implementation in the event-handling code, so I throw a warning.

== Reflections

* Entities can also subscribe to certain types of events, making event-handling for large numbers of events more efficient.
* Making event handlers functional is just a matter of setting up parameters properly.
I'll talk about this in a future post.
* Events could have priority - for example quit event takes priority over everything.
This can be done by using a priority queue instead of a fifo, and assigning priorities when creating events.
* Events may have some common data: timestamp, priority, when/where/how they were created and by whom.
Events could inherit from a base event structure.
* Events might be "consumed" by entities.
This can be implemented by *-handle-event functions returning a boolean indicating whether the event was consumed.
See https://en.wikipedia.org/wiki/Chain-of-responsibility_pattern[chain of responsibility].

.Example Base Event Structure
[source,lisp,linenums]
----
(defstruct event
  timestamp
  priority
  creator)

(defstruct (event-bullet-collision :include event)
  entity
  bullet)
----
